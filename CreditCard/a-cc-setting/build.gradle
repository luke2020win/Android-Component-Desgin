apply plugin: 'com.jakewharton.butterknife'
if (isApp.toBoolean()) {
    apply plugin: 'com.android.application'
} else {
    apply plugin: 'com.android.library'
}


android {
    compileSdkVersion rootProject.ext.compileSdkVersion
    buildToolsVersion rootProject.ext.buildToolsVersion

    defaultConfig {
        minSdkVersion rootProject.ext.minSdkVersion
        targetSdkVersion rootProject.ext.targetSdkVersion

        if (isApp.toBoolean()) {
            applicationId "com.freebank.setting"
            versionCode 1
            versionName "1.0"
        }
        else {
            //ARouter
            javaCompileOptions {
                annotationProcessorOptions {
                    arguments = [AROUTER_MODULE_NAME: project.getName()]
                }
            }
        }

        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"

    }

    sourceSets {
        main {
            if (!isApp.toBoolean()) {
                manifest.srcFile 'src/main/module/AndroidManifest.xml'
            } else {
                manifest.srcFile 'src/main/AndroidManifest.xml'
                //集成开发模式下排除debug文件夹中的所有Java文件
                java {
                    exclude 'com.freebank.setting.debug/**'
                }
            }
        }
    }

    buildTypes {
        debug {
            //测试地址
            buildConfigField "String", "BASE_URL", "\"http://dev.xiaohuaniu.vip/\""
            //DEBUG
            buildConfigField "Boolean", "IS_DEBUG", "true"
        }

        release {
            //正式地址
            buildConfigField "String", "BASE_URL", "\"http://card.xiaohuaniu.vip/\""
            //DEBUG
            buildConfigField "Boolean", "IS_DEBUG", "false"

            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    lintOptions {
        abortOnError false
    }

    compileOptions {
        sourceCompatibility rootProject.ext.javaVersion
        targetCompatibility rootProject.ext.javaVersion
    }
}

dependencies {
    if (!isApp.toBoolean()) {
        implementation project(':c-router')
        annotationProcessor "com.alibaba:arouter-compiler:${rootProject.ext.arouterCompiler}"
    }
    implementation project(':c-common-tools')
    implementation project(':c-framework')
    implementation project(':c-network-http')
    implementation project(':c-define-processor')
}
